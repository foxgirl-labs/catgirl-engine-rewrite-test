// TODO: Rewrite as Kotlin DSL - https://developer.android.com/build/migrate-to-kotlin-dsl
plugins {
    id 'org.jetbrains.kotlin.android'
}

def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary

// ----------------------------------------
// Read Version Info From Cargo.toml
// ----------------------------------------
// https://docs.oracle.com/javase/8/docs/api/java/io/File.html
File cargoTomlLocation = new File(buildscript.sourceFile.parentFile.parentFile.parentFile, "Cargo.toml")
ArrayList<String> cargoTomlContents = cargoTomlLocation.readLines()
String cargoVersionLine = cargoTomlContents.find({ line -> line.toLowerCase().startsWith("version") })
String cargoVersion = cargoVersionLine.split("\"", 3)[1]
String[] cargoVersionSplit = cargoVersion.split('\\.', 3)

// TODO: Figure out how to deal with 0.40.0 and 4.0.0
int cargoVersionCode = Integer.parseInt(cargoVersionSplit[0]) * 1000000 +
                        Integer.parseInt(cargoVersionSplit[1]) * 1000 +
                        Integer.parseInt(cargoVersionSplit[2]) * 1
println "Building Catgirl Engine v" + cargoVersion + " (" + cargoVersionCode + ")"
// ----------------------------------------

if (buildAsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace 'land.catgirl.engine'
    compileSdk 34

    defaultConfig {
        if (buildAsApplication) {
            applicationId "land.catgirl.engine"
        }

        minSdk 34
        targetSdk 34
        versionName cargoVersion
        versionCode cargoVersionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        // The if statement is to tell IDEs to shut up about the release key
        if (System.env.ANDROID_RELEASE_KEY_PATH) {
            release {
                storeFile file(System.env.ANDROID_RELEASE_KEY_PATH)
                storePassword System.env.ANDROID_RELEASE_STORE_PASSWORD
                keyAlias "key0"
                keyPassword System.env.ANDROID_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (System.env.ANDROID_RELEASE_KEY_PATH)
                signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        prefab true
    }
}

apply plugin: 'com.github.willir.rust.cargo-ndk-android'

cargoNdk {
    targets = ["arm64", "arm", "x86", "x86_64"]
    module = ".."
    extraCargoBuildArguments = ["--lib"]
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.games:games-activity:2.0.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}